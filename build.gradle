apply plugin:'scala'
apply plugin:'maven'
apply plugin:'signing'
description='A simple JDBC interface for Scala'
group='com.solab.scalasql'
version='0.9'
defaultTasks 'build'

repositories {
	mavenCentral()
	mavenRepo url:'http://scala-tools.org/repo-releases'
}

dependencies {
	scalaTools 'org.scala-lang:scala-library:2.9.1', 'org.scala-lang:scala-compiler:2.9.1'
	compile 'org.scala-lang:scala-library:2.9.1'
	testCompile 'com.h2database:h2:1.3.162', 'commons-dbcp:commons-dbcp:1.4',
		'junit:junit:4.10', 'org.specs2:specs2_2.9.1:1.7'
}

//tasks.scaladoc.scalaDocOptions.use=true
//tasks.scaladoc.scalaDocOptions.additionalParameters.links=['http://download.oracle.com/javase/6/docs/api/', 'http://www.scala-lang.org/api/current/']

task javadocJar(type:Jar, dependsOn:'scaladoc') {
	from scaladoc.destinationDir
	classifier='javadoc'
}
task sourcesJar(type:Jar) {
	from sourceSets.main.allSource
	classifier='sources'
}

artifacts {
	archives jar, sourcesJar, javadocJar
}
signing {
	sign configurations.archives
}

deployUsernameProperty = "nexusUsername"
deployPasswordProperty = "nexusPassword"

uploadArchives {
	repositories {
		project.deployer=repositories.mavenDeployer {
			beforeDeployment { MavenDeployment deployment ->
				def signedPomArtifact=signPom(deployment)
				deployment.addArtifact(signedPomArtifact)
			}
			repository(url:'http://oss.sonatype.org/service/local/staging/deploy/maven2/') {
				authentication(userName:nexusUsername, password:nexusPassword)
			}
			pom.project {
				name 'ScalaSQL'
				packaging 'jar'
				description project.description
				url 'http://github.com/chochos/scalasql'
				inceptionYear '2011'
				scm {
					url 'scm:git://github.com/chochos/ScalaSQL.git'
					connection 'scm:git://github.com/chochos/ScalaSQL.git'
					developerConnection 'scm:git://github.com/chochos/ScalaSQL.git'
				}
				licenses {
					license {
						name 'The Apache Software License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution 'repo'
					}
				}
				developers {
					developer {
						id 'chochos'
						name 'Enrique Zamudio Lopez'
						url 'http://javamexico.org/blogs/ezamudio'
						roles {
							role 'Architect'
							role 'Developer'
						}
						properties {
							twitter 'chochosmx'
						}
						timezone 'America/Mexico_City'
					}
				}
			}
		}
	}
}
